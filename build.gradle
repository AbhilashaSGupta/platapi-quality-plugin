buildscript {
    repositories {
        maven {
            credentials {
                username ci_pull_user
                password ci_pull_password
            }
            url 'https://artifactory.secureserver.net/artifactory/java-virt'
        }
    }
    dependencies {
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:latest.release"
    }
}

plugins {
    id 'java-gradle-plugin'
    id 'groovy'
    id 'jacoco'
    id 'ru.vyarus.quality' version '4.3.0'
    id 'ru.vyarus.mkdocs' version '2.0.1'
    id "com.gradle.plugin-publish" version '0.11.0'
    id 'maven-publish'
}

apply plugin: "com.jfrog.artifactory"

repositories {
    gradlePluginPortal()
    maven {
        credentials {
            username ci_pull_user
            password ci_pull_password
        }
        url "https://artifactory.secureserver.net/artifactory/java-virt"
    }
}

sourceCompatibility = 1.8

project.version '0.1.4'
project.group 'com.godaddy.platapi.quality'
project.description = 'Gradle quality checks plugin for Platapi team'

dependencies {
    implementation group: 'gradle.plugin.com.github.spotbugs.snom', name: 'spotbugs-gradle-plugin', version: '4.4.4'
    implementation 'org.owasp:dependency-check-gradle:5.3.2'
    implementation group: 'com.google.guava', name: 'guava', version: '29.0-jre'
    implementation "com.github.ben-manes:gradle-versions-plugin:0.27.0"

}


mkdocs.publish {
    docPath = '4.3.0'
    rootRedirect = true
}


wrapper {
    gradleVersion = '6.5.1'
}

gradlePlugin {
    plugins {
        platapiPlugin.id = 'com.godaddy.platapi.quality'
        platapiPlugin.displayName = project.description
        platapiPlugin.implementationClass = 'com.godaddy.platapi.quality.EntryPoint'
    }
}

pluginBundle {
    description = 'Common build steps for the platapi projects'
    tags = ['java', 'groovy', 'quality', 'checkstyle', 'pmd', 'cpd', 'spotbugs', 'codenarc']

    mavenCoordinates {
        groupId = project.group
        artifactId = project.name
    }
}

quality {
    strict = false
}

artifactory {
    contextUrl = 'https://artifactory.secureserver.net/artifactory'
    publish {
        repository {
            repoKey = 'maven-api-customers-platapi-quality-plugin-local'

            // The username/password should be the credentials for the vertx-customers effort in openstack.
            // The ci_api_customers_user and ci_api_customers_password is pulled from your ~/.gradle/gradle.properties file.

            username = artifactory_publish_user
            password = artifactory_publish_password
            maven = true
            defaults {
                publications('PlatapiQualityPlugin')
            }
        }
    }
}

publishing {
    publications {
        PlatapiQualityPlugin(MavenPublication) {
            from components.java
        }
    }
}

